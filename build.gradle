buildscript {
	repositories {
		jcenter()
		maven {
			url "http://files.minecraftforge.net/maven"
		}
	}
	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
	}
}

apply plugin: "net.minecraftforge.gradle.forge"

version = "${modVersion}-${minecraftVersion}"
group = "me.paulf.rbeacons"
archivesBaseName = "responsivebeacons"
sourceCompatibility = JavaVersion.VERSION_1_8

minecraft {
	version = "${project.minecraftVersion}-${project.forgeVersion}"
	runDir = "run"
	mappings = project.mcpVersion
	makeObfSourceJar = false
}

sourceSets {
	main {
		// workaround for FML DirectoryDiscoverer limitations
		output.resourcesDir = output.classesDir
	}
}

processResources {
	def vars = [
		"version": project.modVersion,
		"forgeVersion": project.forgeVersion
	]
	inputs.properties(vars)
	from("src/main/template-resources") {
		expand(vars)
	}
}

// workaround for workaround for FML DirectoryDiscoverer limitations breaking jar task from duplicated resources
jar {
	doFirst {
		ext.outputDir = sourceSets.main.output.resourcesDir
		sourceSets.main.output.resourcesDir = "/dev/null"
	}
	doLast {
		sourceSets.main.output.resourcesDir = ext.outputDir
	}
}
